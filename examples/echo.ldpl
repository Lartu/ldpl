# TCP/IP Echo Server & Client
# by Martin del Rio
# https://www.ldpl-lang.org/
# Created for LDPL 1.0

DATA:
host is text
port is number
data is text
msg is text
request is text map
response is text

PROCEDURE:
store "localhost" in host
store 7668 in port

sub-procedure callback
    trim request:"data" in data
    if data is equal to "quit" then
        reply with "bye!"
        reply with crlf
        close connection to request:"ip"
    else
        display "Got: " data crlf
        join data and crlf in data
        join "You sent: " and data in data
        reply with data
    end if
end sub-procedure

if argv:0 is equal to "--server" then
    display ">> Listening on " host " at " port "..." crlf
    listen on host at port and call callback with request
	if errorcode is not equal to 0 then
	  display "\e[1;31mError:\e[0m " errortext crlf
	end if
else if argv:0 is equal to "--client" then
    connect to host at port
    while errorcode is equal to 0 do
        display ">> "
        accept msg
        send msg to host at port in response
        display "<< " response
        trim response in response
        if response is equal to "bye!" then
            break
        end if
    repeat
    close connection

    if errorcode is not equal to 0 then
        display errortext crlf
    end if
else
    display "usage: echo [--server|--client]" crlf
end if
