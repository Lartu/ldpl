# TCP/IP Echo Server & Client
# by Martin del Rio
# https://www.ldpl-lang.org/
# Created for LDPL 1.0

DATA:
host is text
port is number
msg is text
request is text map
response is text

PROCEDURE:
store "localhost" in host
store 7668 in port

sub-procedure callback
    display "Got: " request:"data" crlf
    join "You sent: " and request:"data" in request:"data"
    reply with request:"data"
end sub-procedure

if argv:0 is equal to "--server" then
    display ">> Listening on " host " at " port "..." crlf
    listen on host at port and call callback with request

else if argv:0 is equal to "--client" then
    while 1 is equal to 1 do
        display ">> "
        accept msg
        send msg to host at port in response
        display "<< " response crlf
    repeat

else
    display "usage: echo [--server|--client]" crlf
end if
